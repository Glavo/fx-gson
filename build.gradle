plugins {
    id("com.jfrog.bintray") version "1.8.4"
    id("org.hildan.github.changelog") version "0.8.0"
    id("org.openjfx.javafxplugin") version "0.0.9"
}

apply plugin: 'java-library'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

group = 'org.hildan.fxgson'
description = 'A set of type adapters for Google Gson to make JavaFX properties serialization more natural.'
ext.labels = ['json', 'gson', 'javafx', 'property']
ext.licenses = ['MIT']

java {
    withJavadocJar()
    withSourcesJar()
}

javafx {
    version = "11"
    modules = [ "javafx.base", "javafx.graphics" ]
}

repositories {
    mavenCentral()
}

configurations {
    checkstyleConfig
}

dependencies {
    api("com.google.code.gson:gson:2.8.6")
    implementation("org.jetbrains:annotations:18.0.0")
    testImplementation("junit:junit:4.+")
    checkstyleConfig("org.hildan.checkstyle:checkstyle-config:2.4.1")
}

checkstyle {
    maxWarnings = 0
    toolVersion = "8.29"
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, "checkstyle.xml")
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
            }
        }
    }
}
check.dependsOn(jacocoTestCoverageVerification)

def getPropOrEnv = { String prop, String envVar, String defaultValue = null ->
    if (project.hasProperty(prop)) {
        return project.property(prop)
    }
    return System.getenv(envVar) ?: defaultValue
}

ext {
    githubUser = getPropOrEnv('githubUser', 'GITHUB_USER')
    githubRepoName = project.name
    githubSlug = "$githubUser/$githubRepoName"
    githubRepoUrl = "https://github.com/$githubSlug"
    ossrhUserToken = getPropOrEnv('ossrhUserToken', 'OSSRH_USER_TOKEN')
    ossrhKey = getPropOrEnv('ossrhKey', 'OSSRH_KEY')
}

def pomConfig = {
    scm {
        url githubRepoUrl
        connection 'scm:git:' + githubRepoUrl + '.git'
        developerConnection 'scm:git:git@github.com:' + githubSlug + '.git'
    }
    licenses {
        license {
            name 'The MIT License'
            url 'https://opensource.org/licenses/MIT'
        }
    }
    developers {
        developer {
            name 'Joffrey Bion'
            email 'joffrey.bion@gmail.com'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', project.description)
                root.appendNode('url', githubRepoUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = getPropOrEnv('bintrayUser', 'BINTRAY_USER')
    key = getPropOrEnv('bintrayApiKey', 'BINTRAY_KEY')
    publications = ['maven']
    publish = true

    pkg {
        repo = getPropOrEnv('bintrayRepo', 'BINTRAY_REPO')
        name = project.name
        desc = project.description
        labels = project.labels ?: [project.name]
        licenses = project.licenses ?: []

        websiteUrl = githubRepoUrl
        issueTrackerUrl = githubRepoUrl + '/issues'
        vcsUrl = githubRepoUrl + '.git'
        githubRepo = githubSlug

        version {
            desc = project.description
            vcsTag = 'v' + project.version
            gpg { sign = true }
            mavenCentralSync {
                sync = true
                user = ossrhUserToken
                password = ossrhKey
            }
        }
    }
}

bintrayUpload.dependsOn(build)

changelog {
    futureVersionTag = project.version
    sinceTag = "v1.2"
    releaseUrlTemplate = "https://bintray.com/joffrey-bion/maven/fx-gson/%s"
    releaseUrlTagTransform = { it.substring(1) }
    customTagByIssueNumber = [12: "v3.1.0"]
}
